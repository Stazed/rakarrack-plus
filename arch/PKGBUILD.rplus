# Maintainer: Your name here <your@email.here>
# To use: makepkg -p PKGBUILD.rplus -si
pkgname=rakarrack-plus
pkgver=1.1.1.10.gd7f44153
pkgrel=1
pkgdesc="Stereo digital effects processor with Jack support (Continuation of rakarrack, fixes & more, git version)"
arch=('i686' 'x86_64' 'aarch64')
url="https://github.com/Stazed/rakarrack-plus"
license=('GPL3')
depends=('alsa-utils' 'fftw' 'fltk' 'jack' 'libxpm' 'liblo')
makedepends=('git' 'cmake')
provides=("${pkgname}")
conflicts=("${pkgname}" 'rakarrack-plus-ALL')

source=("git+${url}.git#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname}"
  git describe --long --tags | sed 's/Release-//;s/-/./g'
}

build(){
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"
  cmake "$srcdir/${pkgname}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DBuildLV2Plugins=OFF \
        -DBuildRakarrackPlusLV2=OFF \
        -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
}
